<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <link rel="stylesheet" href="./background.css">
    <script id="waves-vs" type="x-shader/x-vertex">
        precision mediump float;

        uniform float time;
        uniform mat4 modelViewMatrix;
        uniform mat4 projectionMatrix;

        const int num_waves = 4;
        uniform struct Wave {
            vec2 direction;
            float amplitude;
            float length;
            float speed;
            float frequency;
            float steepness;
        } waves[4];

        attribute vec3 position;

        varying vec3 vPos;

        // Fancy math referenced from https://developer.nvidia.com/gpugems/gpugems/part-i-natural-effects/chapter-1-effective-water-simulation-physical-models
        vec3 gerstnerWaves(vec3 position, float time) {
            vec3 summedWaves = position;
            for (int i = 0; i < num_waves; i++) {
                float phase = (waves[i].speed * 2.0 / waves[i].length) * time;
                float theta = dot((waves[i].direction), position.xz) * waves[i].frequency + phase;
                
                // Height
                summedWaves.y += waves[i].amplitude * sin(theta);

                float horizontal = waves[i].steepness * waves[i].amplitude * cos(theta);
                summedWaves.x += waves[i].direction.x * horizontal;
                summedWaves.z += waves[i].direction.y * horizontal;
            }

            return summedWaves;
        }

        vec3 sumOfSineWaves(vec3 position, float time) {
            vec3 summedWaves = position;
            for (int i = 0; i < num_waves; i++) {
                float phase = (waves[i].speed * 2.0 / waves[i].length) * time;
                float theta = dot(waves[i].direction, position.xz) * waves[i].frequency + phase;
                summedWaves.y += waves[i].amplitude * sin(theta);
            }
            return summedWaves;
        }

        void main() {
            vPos = gerstnerWaves(position, time);
            //vPos = sumOfSineWaves(position, time);
            gl_Position = projectionMatrix * modelViewMatrix * vec4(vPos, 1.0);
        }
    </script>
    <script id="waves-fs" type="x-shader/x-fragment">
        precision mediump float;
        
        const vec3 cameraPos = vec3(0.0, 1.0, 10.0);
        const vec3 fogColor = vec3(0.0745,0.05098,0.12549);
        uniform float sineTime; 

        varying vec3 vPos;

        // Functions rgb2hsv() and hsv2rgb() are from http://lolengine.net/blog/2013/07/27/rgb-to-hsv-in-glsl
        vec3 rgb2hsv(vec3 c) {
            vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
            vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
            vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

            float d = q.x - min(q.w, q.y);
            float e = 1.0e-10;
            return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
        }

        vec3 hsv2rgb(vec3 c) {
            vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
            vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
            return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
        }

        float fogAmount(vec3 position) {
            return 1.0 - exp(-length(position - cameraPos) * 0.25);
        }

        vec3 addFog(vec3 color) {
            return mix(color, fogColor, fogAmount(vPos));
        }

        void main() {
            vec3 color = vec3(0.0, 0.0, 1.0);
            color.b += vPos.y;
            vec3 hsv = rgb2hsv(color);
            hsv.r = (hsv.r + sineTime + (vPos.x / 50.0)) / 1.0;
            gl_FragColor = vec4(addFog(hsv2rgb(hsv)), 1.0);
        }
    </script>
    <script type="module" src="./background.js" defer></script>

    <title>Background - Jakob Strobl</title>
</head>
<body>
</body>
</html>